---
layout: ""
page_title: "Provider: Qdrant Cloud"
description: |-
  The Qdrant Cloud Terraform provider allows full lifecycle management of Qdrant Cloud resources.
weight: 100
---

# Qdrant Cloud Provider

The Terraform Qdrant Cloud provider is a plugin for Terraform that allows for the full lifecycle management of Qdrant Cloud resources.
Note that it's not intended to manage the content of the database itself.

Below is a sample that creates a [vector database] cluster and a token for accessing the cluster.

The ID and version of the cluster as well the URL (endpoint of the database cluster) are displayed (as output).

The access token created is scoped for the created cluster and is displayed as well.
Note that this token should be kept secret, with this token the database can be manipulated and viewed (CRUD operations).

To view the cluster itself, please visit (in a web-browser)
`<cluster_url>:6333/dashboard`
This will ask the token to grant you access.

The url and token can be used in client libraries as well

For more info, please visit https://qdrant.tech/

---

## Versioning and Compatibility

Please always use the latest version of the provider.

Versions below `1.1.0` are deprecated and should not be used.

---

## Example Usage

```terraform
terraform {
  required_version = ">= 1.7.0"
  required_providers {
    qdrant-cloud = {
      source  = "qdrant/qdrant-cloud"
      version = ">=1.1.0"
    }
  }
}

provider "qdrant-cloud" {
  api_key    = "" // API Key generated in Qdrant Cloud (required)
  api_url    = "" // URL where the public API of Qdrant cloud can be found (optional: defaults to production URL).
  account_id = "" // The default account ID you want to use in Qdrant Cloud (can be overriden on resource level)
}

resource "qdrant-cloud_accounts_cluster" "example" {
  name           = "tf-example-cluster"
  cloud_provider = "gcp"
  cloud_region   = "us-east4"
  configuration {
    number_of_nodes = 1
    node_configuration {
      package_id = "7c939d96-d671-4051-aa16-3b8b7130fa42" # gpx1
    }
  }
}

resource "qdrant-cloud_accounts_auth_key" "example-key" {
  cluster_ids = [qdrant-cloud_accounts_cluster.example.id]
}

output "cluster_id" {
  value = qdrant-cloud_accounts_cluster.example.id
}

output "url" {
  value = qdrant-cloud_accounts_cluster.example.url
}

output "token" {
  value       = qdrant-cloud_accounts_auth_key.example-key.token
  description = "Token is available only once, after creation."
}

output "curl_command" {
  value       = "curl \\\n    -X GET '${qdrant-cloud_accounts_cluster.example.url}' \\\n    --header 'api-key: ${qdrant-cloud_accounts_auth_key.example-key.token}'"
  description = "Generating a curl command test cluster access using the API key."
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `api_key` (String, Sensitive) The API Key for Qdrant Cloud API operations.

### Optional

- `account_id` (String) Default Account Identifier for the Qdrant cloud
- `api_url` (String) The URL of the Qdrant Cloud API.
- `insecure` (Boolean) Allow insecure gRPC connections. This is useful for development environments with self-signed certificates. Defaults to false.